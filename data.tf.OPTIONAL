data "aws_iam_policy_document" "dataworks_aws_corporate_data_ingestion_write_data" {
  statement {
    effect = "Allow"

    actions = [
      "s3:GetBucketLocation",
      "s3:ListBucket",
    ]

    resources = [
      data.terraform_remote_state.common.outputs.published_bucket.arn,
    ]
  }

  statement {
    effect = "Allow"

    actions = [
      "s3:Get*",
      "s3:List*",
      "s3:DeleteObject*",
      "s3:Put*",
    ]

    resources = [
      "${data.terraform_remote_state.common.outputs.published_bucket.arn}/pdm-dataset/*",
      "${data.terraform_remote_state.common.outputs.published_bucket.arn}/metrics/*",
      "${data.terraform_remote_state.common.outputs.published_bucket.arn}/common-model-inputs/*",
      "${data.terraform_remote_state.common.outputs.published_bucket.arn}/analytical-dataset/*",
      "${data.terraform_remote_state.common.outputs.published_bucket.arn}/data",
      "${data.terraform_remote_state.common.outputs.published_bucket.arn}/data/*",
    ]
  }

  statement {
    effect = "Allow"

    actions = [
      "kms:Encrypt",
      "kms:Decrypt",
      "kms:ReEncrypt*",
      "kms:GenerateDataKey*",
      "kms:DescribeKey",
    ]

    resources = [
      "${data.terraform_remote_state.common.outputs.published_bucket_cmk.arn}",
    ]
  }
}

resource "aws_iam_policy" "dataworks_aws_corporate_data_ingestion_write_data" {
  name        = "AwsEMRWriteData"
  description = "Allow writing of aws-emr-template files and metrics"
  policy      = data.aws_iam_policy_document.dataworks_aws_corporate_data_ingestion_write_data.json
}

resource "aws_iam_role_policy_attachment" "dataworks_aws_corporate_data_ingestion_write_data" {
  role       = aws_iam_role.dataworks_aws_corporate_data_ingestion_emr_service.name
  policy_arn = aws_iam_policy.dataworks_aws_corporate_data_ingestion_write_data.arn
}

